plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'com.github.ben-manes.versions'
    id 'eclipse'
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'

dependencies {
    // Spring Framework
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'io.micrometer:micrometer-core'

    // SMTP test server
    implementation 'com.github.davidmoten:subethasmtp:7.2.0'

    // DNS lookup
    implementation 'dnsjava:dnsjava:3.6.3'

    // Jakarta Mail API (email parsing)
    implementation 'com.sun.mail:jakarta.mail:2.0.2'

    // DKIM validation module
    implementation 'org.simplejavamail:dkim-module:8.12.6'

    // Apache Commons Codec
    implementation 'commons-codec:commons-codec:1.15'
}

bootRun {
    def rootDir = rootProject.projectDir
    def profile = "default"

    jvmArgs = [
            "-Dspring.profiles.active=${profile}",
            "-Dspring.devtools.livereload.enabled=true",
            "-Dfile.encoding=UTF-8"
    ]

    args = [
            "--spring.config.location=file:${rootDir}/conf/simulator/"
    ]

    workingDir = rootDir
}

// Checkstyle Configuration
checkstyle {
    ignoreFailures = true
    configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
    maxWarnings = 50
}

checkstyleMain {
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyleTest {
    enabled = false
}

// PMD Configuration
pmd {
    ignoreFailures = true
    ruleSetFiles = files("${projectDir}/config/pmd/pmd-ruleset.xml")
    rulesMinimumPriority.set(5)
}

pmdMain {
    reports {
        xml.required = true
        html.required = true
    }
}

pmdTest {
    enabled = false
}
